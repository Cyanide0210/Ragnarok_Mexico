//===== rAthena Script =======================================
//= Stylist
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Changes your hair style, hair color, and cloth color.
//===== Additional Comments: =================================  
//= 1.1 Switched to 'getbattleflag', credits to Saithis. [Euphy]
//============================================================

prontera,165,170,1	script	Stylist#custom_stylist	122,{
	mes "Hola!";
	next;
	mes "Yo puedo cambiar tu estilo completamente";
	sleep2 800;
	mes "Escoje lo que deseas cambiar";
	setarray .@Styles[1],getbattleflag("max_cloth_color"),getbattleflag("max_hair_style"),getbattleflag("max_hair_color");
	setarray .@Look[1],7,1,6;
	set .@s, select(" ~ Color de Ropa: ~ Estilo de Pelo: ~ Color de Pelo");
	set .@Revert, getlook(.@Look[.@s]); set .@Style,1;
	while(1) {
		setlook .@Look[.@s], .@Style;
		message strcharinfo(0),"Este es el estilo #"+.@Style+".";
		set .@menu$, " ~ Siguiente (^0055FF"+((.@Style!=.@Styles[.@s])?.@Style+1:1)+"^000000): ~ Anterior (^0055FF"+((.@Style!=1)?.@Style-1:.@Styles[.@s])+"^000000): ~ Saltar a...: ~ Revertir al original (^0055FF"+.@Revert+"^000000)";
		switch(select(.@menu$)) {
			case 1: set .@Style, ((.@Style!=.@Styles[.@s])?.@Style+1:1); break;
			case 2: set .@Style, ((.@Style!=1)?.@Style-1:.@Styles[.@s]); break;
			case 3: message strcharinfo(0),"Escoje un estilo entre 1 - "+.@Styles[.@s]+".";
				input .@Style,0,.@Styles[.@s];
				if (!.@Style) set .@Style, rand(1,.@Styles[.@s]);
				break;
			case 4: set .@Style, .@Revert; setlook .@Look[.@s], .@Revert; break; }
	} end;
}
