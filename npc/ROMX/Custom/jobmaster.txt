//===== rAthena Script =======================================
//= Job Master
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN r16114+
//===== Description: =========================================
//= A fully functional job changer.
//===== Additional Comments: =================================  
//= 1.1 Fixed reset on Baby job change. [Euphy]
//============================================================

prontera,147,175,5	script	Baby Class	909,{
mes "Hola!";
next;
mes "Yo puedo cambiarte a clase baby sin necesidad de tener padres, que te parece?";
next;
mes "Lo unico que necesito es que seas nivel base y job 1 para poderte cambiar ^^'";
next;
mes "De ahi en adelante podras usar el jobchanger original para cambiar de clase.";
mes "Quieres cambiar a clase baby?";
switch(select("No:Si")){
case 1:
mes "Muy bien, te estare esperando!"; close;
case 2:
mes "Ok, dejame reviso si cumples los requerimentos.."; next; 
if (BaseLevel == 1 && JobLevel == 1){
mes "Parece que si cumples con los requerimentos, quieres cambiar?";
switch(select("No:Si")){
case 1: mes "Te estare esperando!"; close;
case 2: atcommand "@storeall"; atcommand "@job 4023"; atcommand "@streset"; atcommand "@skreset"; mes "Listo, asegurate de sacar los objetos que necesitaras del almacen"; close;
} 
} mes "No cumples con los requerimentos..."; close;
}
}

prontera,153,193,6	script	Job Master	123,{
function Job_Menu; function A_An;

	mes "[Job Master]";
	if (Class > 4049) {
		mes "No hay mas empleos disponibles.";
		close; }
	if (checkfalcon() || checkcart() || checkriding() || ismounting()) {
		mes "Porfavor quitate tu "+((checkfalcon())?"falcon":"")+((checkcart())?"cart":"")+((checkriding())?"Peco":"")+((ismounting())?"mount":"")+" antes de proceder.";
		close; }
	if (.SkillPointCheck && SkillPoint) {
		mes "Porfavor usa todos tus puntos de habilidad antes de proseguir.";
		close; }

	set .@eac, eaclass();
	set .@i, ((.ThirdClass)?roclass(.@eac&EAJ_UPPERMASK):Class);
	if (.@i > 6 && .@i < 22) {
		if (BaseLevel < .Rebirth[0] || JobLevel < .Rebirth[1]) {
			set .@blvl, .Rebirth[0]-BaseLevel; set .@jlvl, .Rebirth[1]-JobLevel;
			mes "Necesitas "+((.@blvl>0)?.@blvl+" niveles base mas "+((.@jlvl>0)?"/ ":""):"")+((.@jlvl>0)?.@jlvl+" niveles empleo mas ":"")+"para continuar.";
			close; }
		if (Class > 21) {
			mes "Cambiar a tercera clase?";
			next;
			Job_Menu(roclass(.@eac|EAJL_THIRD));
			close; }
		while(1) {
			mes "Selecciona una opcion.";
			next;
			set .@i, select(" ~ ^0055FFRenacer^000000:"+((.ThirdClass)?" ~ ^FF0000Tercera Clase^000000":"")+": ~ ^777777Cancelar^000000");
			if (.@i==3) close;
			mes "[Job Master]";
			if ( Sex==0 )
			mes "Estas segura?";
			else
			mes "Estas seguro?";
			next;
			Job_Menu(((.@i==1)?4001:roclass(.@eac|EAJL_THIRD)));
			mes "[Job Master]"; } }
	set .@j1, roclass(.@eac|EAJL_2_1); set .@j2,roclass(.@eac|EAJL_2_2);
	if (.@eac&EAJL_2)
		if (.@eac&(EAJL_UPPER|EAJL_BABY) || roclass(.@eac|EAJL_UPPER) == -1) {
			mes "No hay mas empleos disponibles.";
			close; }
	if ((.@eac&EAJ_BASEMASK) == EAJ_NOVICE) {
		if (JobLevel < .JobReq[0])
			mes "Un nivel de job de "+.JobReq[0]+" es requerido para cambiar a primera clase.";
		else if (Class == 4001 && .LastJob && lastJob) {
			mes "Cambiar de clase ahora?";
			next;
			Job_Menu(roclass((eaclass(lastJob)&EAJ_BASEMASK)|EAJL_UPPER)); }
		else switch(Class) {
			case 0: Job_Menu(1,2,3,4,5,6,23,4046,24,25,4023);
			case 4001: Job_Menu(4002,4003,4004,4005,4006,4007);
			case 4023: Job_Menu(4024,4025,4026,4027,4028,4029,4045);
			default: mes "Un error ha ocurrido."; break; }
		close; }
	if (roclass(.@eac|EAJL_2_1) == -1 || roclass(.@eac|EAJL_2_2) == -1)
		mes "No hay mas empleos disponibles.";
	else if (!(.@eac&EAJL_2) && JobLevel < .JobReq[1])
		mes "Un nivel de job de "+.JobReq[1]+" es requerido para cambiar a segunda clase.";
	else if (.LastJob && lastJob && (.@eac&EAJL_UPPER)) {
		mes "Cambiar de clase ahora?";
		next;
		Job_Menu(lastJob+4001); }
	else Job_Menu(.@j1,.@j2);
	close;

function Job_Menu {
	while(1) {
		if (getargcount() > 1) {
			mes "Selecciona un empleo.";
			set .@menu$,"";
			for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
				set .@menu$, .@menu$+" ~ "+jobname(getarg(.@i))+":";
			set .@menu$, .@menu$+" ~ ^777777Cancel^000000";
			next;
			set .@i, getarg(select(.@menu$)-1,0);
			if (!.@i) close;
			if ((.@i == 23 || .@i == 4045) && BaseLevel < .SNovice) {
				mes "[Job Master]";
				mes "Un nivel base de "+.SNovice+" es requerido para cambiar a "+jobname(.@i)+".";
				close; }
			mes "[Job Master]";
			if ( Sex==0 )
			mes "Estas segura?";
			else
			mes "Estas seguro?";
			next; }
		else set .@i, getarg(0);
		if (select(" ~ Cambiar a la clase ^0055FF"+jobname(.@i)+"^000000: ~ ^777777Regresar^000000")==1) {
			mes "[Job Master]";
			mes "Ahora eres "+A_An(jobname(.@i))+"!";
			if (.@i==4001 && .LastJob) set lastJob, Class;
			jobchange .@i;
			if (.@i==4001 || .@i==4023) resetlvl(1);
			specialeffect2 338; specialeffect2 432;
			if (.Platinum) callsub Get_Platinum;
			close; }
		if (getargcount() == 1) return;
		mes "[Job Master]"; }
	end; }

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0); }

Get_Platinum:
	skill 142,1,0;
	switch(BaseClass) {
		case 0: if (Class !=23) skill 143,1,0; break;
		case 1: skill 144,1,0; skill 145,1,0; skill 146,1,0; break;
		case 2: skill 157,1,0; break;
		case 3: skill 147,1,0; skill 148,1,0; break;
		case 4: skill 156,1,0; break;
		case 5: skill 153,1,0; skill 154,1,0; skill 155,1,0; break;
		case 6: skill 149,1,0; skill 150,1,0; skill 151,1,0; skill 152,1,0; break;
		default: break; }
	switch(roclass(eaclass()&EAJ_UPPERMASK)) {
		case 7: skill 1001,1,0; break;
		case 8: skill 1014,1,0; break;
		case 9: skill 1006,1,0; break;
		case 10: skill 1012,1,0; skill 1013,1,0; break;
		case 11: skill 1009,1,0; break;
		case 12: skill 1003,1,0; skill 1004,1,0; break;
		case 14: skill 1002,1,0; break;
		case 15: skill 1015,1,0; skill 1016,1,0; break;
		case 16: skill 1007,1,0; skill 1008,1,0; skill 1017,1,0; skill 1018,1,0; skill 1019,1,0; break;
		case 17: skill 1005,1,0; break;
		case 18: skill 238,1,0; break;
		case 19: skill 1010,1,0; break;
		case 20: skill 1011,1,0; break;
		default: break; }
	return;

OnInit:
	setarray .Rebirth[0],99,50;	// Minimum base level, job level to rebirth OR change to third class
	setarray .JobReq[0],10,40;	// Minimum job level to turn into 1st class, 2nd class
	set .ThirdClass,0;		// Enable third classes? (1: yes / 0: no)
	set .SNovice,45;		// Minimum base level to turn into Super Novice
	set .LastJob,1;			// Enforce linear class changes? (1: yes / 0: no)
	set .SkillPointCheck,1;		// Force player to use up all skill points? (1: yes / 0: no)
	set .Platinum,1;		// Get platinum skills automatically? (1: yes / 0: no)
	end;
}